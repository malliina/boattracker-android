apply plugin: "com.android.application"

apply plugin: "kotlin-android"

apply plugin: "kotlin-android-extensions"

apply plugin: "kotlin-kapt"

apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion 33
    defaultConfig {
        applicationId "com.malliina.boattracker"
        minSdkVersion 28
        targetSdkVersion 33
        versionCode 68
        versionName "1.3.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        def mapboxAccessToken = System.getenv("MapboxAccessToken") ?: MapboxAccessToken
        buildConfigField("String", "MapboxAccessToken", "\"${mapboxAccessToken}\"")
    }
    signingConfigs {
        release {
            if (System.getenv("CI")) {
                storeFile rootProject.file("keystore.jks")
                storePassword System.getenv("KEYSTORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
            } else {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    namespace "com.malliina.boattracker"
}

repositories {
    mavenCentral()
    google()
    maven {
        url "https://api.mapbox.com/downloads/v2/releases/maven"
        authentication {
            basic(BasicAuthentication)
        }
        credentials {
            // Do not change the username below.
            // This should always be `mapbox` (not your username).
            username = "mapbox"
            // Use the secret token you stored in gradle.properties as the password
            password = System.getenv("MapboxDownloadsToken") ?: project.properties['MAPBOX_DOWNLOADS_TOKEN'] ?: ""
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    def navigationVersion = "2.5.3"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"
    implementation "com.android.volley:volley:1.2.1"
    implementation "com.google.android.gms:play-services-auth:20.4.0"
    implementation "com.google.android.material:material:1.7.0"
    implementation "com.google.firebase:firebase-core:21.1.1"
    implementation "com.google.firebase:firebase-messaging:23.1.0"
    implementation "com.jakewharton.timber:timber:5.0.1"
    implementation "com.mapbox.mapboxsdk:mapbox-android-plugin-annotation-v9:0.9.0"
    implementation "com.mapbox.maps:android:10.9.0"
    def appCenterSdkVersion = "4.3.1"
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"
    implementation "com.neovisionaries:nv-websocket-client:2.9"
    def moshiVersion = "1.14.0"
    implementation "com.squareup.moshi:moshi:${moshiVersion}"
    kapt("com.squareup.moshi:moshi-kotlin-codegen:${moshiVersion}")
    implementation "me.zhanghai.android.materialprogressbar:library:1.6.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    def coroutinesVersion = "1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutinesVersion}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${coroutinesVersion}"

    androidTestImplementation "androidx.test:runner:1.5.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.0"
    testImplementation "junit:junit:4.13.2"
}

apply plugin: "com.google.gms.google-services"
